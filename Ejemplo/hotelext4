(define (domain hotelext4)
  (:requirements :adl :strips :typing :fluents)
  (:types habitacion reserva - object)

  (:predicates 
              (ocupada ?hab - habitacion ?res - reserva)
	            (atendida ?res - reserva)
              (abierta ?hab - habitacion)
	)

  (:functions 
              (numpersona ?o - object)
              (inicio ?res - reserva)
              (final ?res - reserva)
              (costeignorar)
              (costeocupacion)
              (costeabrir)
  )

  (:action abrirhabitacion
    :parameters (?res - reserva ?hab - habitacion)
    :precondition (and (not (atendida ?res)) (not (abierta ?hab)) (<= (numpersona ?res) (numpersona ?hab)) )
    :effect (and (abierta ?hab) (ocupada ?hab ?res) (atendida ?res) (increase (costeocupacion) (- (numpersona ?hab) (numpersona ?res) )) (increase (costeabrir) 1) )
  )  

  (:action asignarhabitacion
    :parameters (?res - reserva ?hab - habitacion)
    :precondition (and (not (atendida ?res)) (abierta ?hab) (<= (numpersona ?res) (numpersona ?hab)) 
      (forall (?rese - reserva) 
      	(or (not (ocupada ?hab ?rese)) (or (>=(inicio ?res)(final ?rese)) (>=(inicio ?rese)(final ?res)) )) 
        
      )
    )  

    :effect (and (atendida ?res) (ocupada ?hab ?res) (increase (costeocupacion) (- (numpersona ?hab) (numpersona ?res))) )
  
  )
  (:action ignorarreserva
  	:parameters (?res - reserva)
  	:precondition (not (atendida ?res))
  	:effect (and (atendida ?res) (increase (costeignorar) 1 ) )
  )
  
)
